#!/bin/zsh
# processing of images
# expects to run in images folder
# prior to run, redact any raw files that need it and put them in src/safe
# This script
# 1. converts any dot files in src/dot to png files in src/safe
# 2. converts any text files in src/sidebars to png files in src/sidebars
# 3. scales png files in src/safe and puts them in src/scaled
# 4. puts borders around png files in src/scaled and puts them in src/bordered
# 5. For each file in src/bordered, determines if same name exists in src/sidebars
#    If it does, then puts them together and copies into tgt.
#    If not, then just copies the bordered file to tgt.
#
# run dot jobs - send files to src/safe
for f in dot/*
do
  echo "Creating image from dot: $f"
  fn=$f:t:r #just the file name without the extension
  dot -Tpng -o"src/safe/$fn.png" "$f"
done

# make images from the sidebar text
# use gif as the temp file as it doesn't put the label in the metadata, 
# as the metadata would an echo in the montage step, below
for f in src/sidebars/*.txt
do
  echo "Creating image from sidebar text: $f"
  fn=$f:t:r
  magick -pointsize 20 -font Helvetica -background khaki \
  -interline-spacing 10 \
  -fill blue label:@src/sidebars/"$fn".txt \
  -bordercolor khaki -border 20x20 \
  src/sidebars/"$fn".gif

  # montage step adds the shadow
  magick montage src/sidebars/"$fn".gif -tile x1 \
  -shadow -geometry +3+5 -background none \
  -alpha set -bordercolor none -border 40x20 \
  src/sidebars/"$fn".png

  #rm src/sidebars/"$fn".gif
done

# scale and put a border around items in src/safe
for f in src/safe/*
do
  echo "Scaling $f"
  fn=$f:t:r
  magick src/safe/"$fn".png -resize 640x640\> \
  src/scaled/"$fn".png  
  #magick "$f" -resize 50% -bordercolor navyblue -border 1 -alpha set \
  #-bordercolor none -border 50x0 "tgt/$f:t"

  # border
  magick src/scaled/"$fn".png -bordercolor navyblue -border 1 \
  -alpha set -bordercolor none -border 40x20 \
  src/bordered/"$fn".png
done

echo "Placing files in tgt/"
for f in src/bordered/*
do
  fn=$f:t:r
  # determine if there is a sidebar
  if [[ -f src/sidebars/"$fn".png ]]
  then
    echo "Smushing with sidebar: $f"
    magick convert -gravity center +smush 40 src/bordered/"$fn".png \
    src/sidebars/"$fn".png tgt/"$fn".png
  else
    echo "Copying $fn.png if changed"
    rsync -c $f "tgt/$f:t" # only copy if changed
  fi
done
